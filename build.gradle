//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id "com.github.johnrengelman.shadow" version "7.1.2"
	id "maven-publish"
	id "java"
}

group = "com.github.frcsty"
version = "2.0.0-SNAPSHOT"

repositories {
	mavenCentral()
	maven { url "https://papermc.io/repo/repository/maven-public/" }
	maven { url "https://repo.triumphteam.dev/snapshots/" }
	maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
}

dependencies {
	implementation "org.jetbrains:annotations:23.0.0"
	implementation "me.mattstudios:triumph-msg-adventure:2.2.4-SNAPSHOT"
	
	compileOnly "io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT"
	compileOnly "me.clip:placeholderapi:2.10.9"
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	
	from(sourceSets.main.resources.srcDirs) {
		filter ReplaceTokens, tokens: [version: version]
	}
}

shadowJar {
	archiveFileName.set("FrozenActions(${project.version}).jar")
}

def targetJavaVersion = 17
java {
	def javaVersion = JavaVersion.toVersion(targetJavaVersion)
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
	if (JavaVersion.current() < javaVersion) {
		toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
	}
}

tasks.withType(JavaCompile).configureEach {
	if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
		options.release = targetJavaVersion
	}
}

processResources {
	def props = [version: version]
	inputs.properties props
	filteringCharset 'UTF-8'
	filesMatching("plugin.yml") {
		expand props
	}
}
